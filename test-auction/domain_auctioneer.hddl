(define (domain minehunting)
  (:requirements :typing :action-costs)
  (:types
    task robot - object
    area-to-cover - task
  )

  (:predicates 
    (allocated ?t - task)
    (resellable ?t - task)
    (bid-on ?r - robot ?t - task)
    (already-busy ?r - robot)
  )

  (:functions
    (bid-value ?r - robot ?t - task) - number
    (resale-value ?t - task) - number
    (total-cost) - number
    )

  (:task cover :parameters (?a - area-to-cover))

  (:method m-cover-allocate
    :parameters (?a - area-to-cover ?r - robot)
    :task (cover ?a)
    :subtasks (allocate-action ?a ?r)
  )

  (:method m-cover-resell
    :parameters (?a - area-to-cover)
    :task (cover ?a)
    :subtasks (resell-action ?a)
  )

  (:action allocate-action
    :parameters (?t - task ?r - robot)
    :precondition (and
      (not (allocated ?t))
      (bid-on ?r ?t)
      (not (already-busy ?r))
      )
    :effect (and
      (allocated ?t)
      (already-busy ?r)
      (increase (total-cost) (bid-value ?r ?t))
      )
  )

  (:action resell-action
    :parameters (?t - task)
    :precondition (and
      (not (allocated ?t))
      (resellable ?t)
      )
    :effect (and
      (allocated ?t)
      (increase (total-cost) (resale-value ?t))
      )
  )
)